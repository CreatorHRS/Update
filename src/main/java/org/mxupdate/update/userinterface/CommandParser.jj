/*
 *  This file is part of MxUpdate <http://www.mxupdate.org>.
 *
 *  MxUpdate is a deployment tool for a PLM platform to handle
 *  administration objects as single update files (configuration item).
 *
 *  Copyright (C) 2008-2016 The MxUpdate Team - All Rights Reserved
 *
 *  You may use, distribute and modify MxUpdate under the terms of the
 *  MxUpdate license. You should have received a copy of the MxUpdate
 *  license with this file. If not, please write to <info@mxupdate.org>,
 *  or visit <www.mxupdate.org>.
 *
 */

options {
    JDK_VERSION = "1.6";
    STATIC = false;
    ERROR_REPORTING = true;
    USER_TOKEN_MANAGER = false;
    GENERATE_BOILERPLATE = false;
    SUPPORT_CLASS_VISIBILITY_PUBLIC = false;
    UNICODE_INPUT = true;
}

PARSER_BEGIN(CommandParser)
package org.mxupdate.update.userinterface;

import java.lang.reflect.InvocationTargetException;

import org.mxupdate.mapping.TypeDef_mxJPO;
import org.mxupdate.update.util.AbstractParser_mxJPO;
import org.mxupdate.update.util.AbstractParser_mxJPO.ParseException;
import org.mxupdate.update.util.AbstractParser_mxJPO.SimpleCharStream;
import org.mxupdate.update.util.AbstractParser_mxJPO.Token;
import org.mxupdate.update.util.AbstractParser_mxJPO.TokenMgrError;
import org.mxupdate.update.util.AdminPropertyList_mxJPO.AdminProperty;

/**
 * Parses the update format for {@link Command_mxJPO}.
 */
@SuppressWarnings("unused")
class CommandParser
    extends AbstractParser_mxJPO
{
}

PARSER_END(CommandParser)

<*> SKIP :
{
  " " | "\t" | "\n" | <("#" (~["\n"])* "\n")>
}

TOKEN :
{
      <SYMBOLICNAME:            "symbolicname">                                 : STRING_EXPECTED
    | <DESCRIPTION:             "description">                                  : MULTILINESTRING_EXPECTED
    | <HIDDEN_TRUE:             "hidden">
    | <HIDDEN_FALSE:            "!hidden">
    | <ALT:                     "alt">                                          : STRING_EXPECTED
    | <HREF:                    "href">                                         : STRING_EXPECTED
    | <LABEL:                   "label">                                        : STRING_EXPECTED
    | <USER:                    ("add" ([" ","\t","\n","\r"])+ )? "user">       : STRING_EXPECTED
    | <SETTING:                 ("add" ([" ","\t","\n","\r"])+ )? "setting">    : SETTING_EXPECTED
    | <CODE:                    "code">                                         : MULTILINESTRING_EXPECTED
}

/************************************************************** Common String */
<STRING_EXPECTED> TOKEN :
{
      <STRING: ( "\"" ( ~["\u0000"-"\u001f","\"","\\","\u0100"-"\uffff"] | "\\\"" | "\\\\" | "\\{" | "\\}" )* "\"" )> : DEFAULT
    | <SINGLE: ( ~["\u0000"-"\u0020","\"","\\","{","}","\u0100"-"\uffff"] )+> : DEFAULT
}

/*************************************************** Common Multi-Line-String */
<MULTILINESTRING_EXPECTED> TOKEN :
{
      <MULTILINESTRING: ( "\"" ( ~["\u0000"-"\u0009","\u000b"-"\u001f","\"","\\","\u0100"-"\uffff"] | "\\\"" | "\\\\" | "\\{" | "\\}" | "\\n" )* "\"" )> : DEFAULT
    | <MULTILINESINGLE: ( ~["\u0000"-"\u0020","\"","\\","{","}","\u0100"-"\uffff"] )+> : DEFAULT
}

/********************************************************* Setting Key String */
<SETTING_EXPECTED> TOKEN :
{
      <SETTING_STRING: ( "\"" ( ~["\u0000"-"\u001f","\"","\\","\u0100"-"\uffff"] | "\\\"" | "\\\\" | "\\{" | "\\}" )* "\"" )> : STRING_EXPECTED
    | <SETTING_SINGLE: ( ~["\u0000"-"\u0020","\"","\\","{","}","\u0100"-"\uffff"] )+> : STRING_EXPECTED
}

/******************************************************************* Property */
<DEFAULT> TOKEN :
{      <PROPERTY:                "property">                                     : STRING_EXPECTED
    | <PROPERTYTO:              "to">                                           : ADMINREF_EXPECTED
    | <PROPERTYVAL:             "value">                                        : STRING_EXPECTED
}

<ADMINREF_EXPECTED> TOKEN :
{
      <ADMINTYPE_STRING : ( "\"" ( ~["\u0000"-"\u001f","\"","\\","\u0100"-"\uffff"] | "\\\"" | "\\\\" | "\\{" | "\\}" )* "\"" )> : STRING_EXPECTED
    | <ADMINTYPE_SINGLE : ( ~["\u0000"-"\u0020","\"","\\","{","}","\u0100"-"\uffff"] )+> : STRING_EXPECTED
}

/**************************************************************************** */

/**
 * Parses one complete command definition.
 *
 * @param _command  target command to update with parsed values
 */
void parse(Command_mxJPO _command)
    throws SecurityException, IllegalArgumentException, NoSuchMethodException, InstantiationException, IllegalAccessException, InvocationTargetException
: {
    Token tmp;
    AdminProperty property;
} {
    (   ( <SYMBOLICNAME>            ( tmp = <STRING>           {this.appendValue(_command, "symbolicNames", this.getString(tmp.image));}
                                    | tmp = <SINGLE>           {this.appendValue(_command, "symbolicNames", this.getSingle(tmp.image));} ) )

      | (<DESCRIPTION>              ( tmp = <MULTILINESTRING>  {this.setValue(_command, "description", this.getString(tmp.image));}
                                    | tmp = <MULTILINESINGLE>  {this.setValue(_command, "description", this.getSingle(tmp.image));} ) )

      | (<HIDDEN_TRUE>                                         {this.setValue(_command, "hidden",       true);} )
      | (<HIDDEN_FALSE>                                        {this.setValue(_command, "hidden",       false);} )

      | (<ALT>                      ( tmp = <STRING>           {this.setValue(_command, "alt", this.getString(tmp.image));}
                                    | tmp = <SINGLE>           {this.setValue(_command, "alt", this.getSingle(tmp.image));} ) )

      | (<HREF>                     ( tmp = <STRING>           {this.setValue(_command, "href", this.getString(tmp.image));}
                                    | tmp = <SINGLE>           {this.setValue(_command, "href", this.getSingle(tmp.image));} ) )

      | (<LABEL>                    ( tmp = <STRING>           {this.setValue(_command, "label", this.getString(tmp.image));}
                                    | tmp = <SINGLE>           {this.setValue(_command, "label", this.getSingle(tmp.image));} ) )

      | (<USER>                     ( tmp = <STRING>           {this.appendValue(_command, "users", this.getString(tmp.image));}
                                    | tmp = <SINGLE>           {this.appendValue(_command, "users", this.getSingle(tmp.image));} ) )

      | (<SETTING>                                             {property = new AdminProperty();this.appendValue(this.getValue(_command, "properties"), "propertiesStack", property);}
                                    ( tmp = <SETTING_STRING>   {this.setValue(property, "name", "%" + this.getString(tmp.image));}
                                    | tmp = <SETTING_SINGLE>   {this.setValue(property, "name", "%" + this.getSingle(tmp.image));})
                                    ( tmp = <STRING>           {this.setValue(property, "value", this.getString(tmp.image));}
                                    | tmp = <SINGLE>           {this.setValue(property, "value", this.getSingle(tmp.image));} ) )

      | (<PROPERTY>                                            {property = new AdminProperty();this.appendValue(this.getValue(_command, "properties"), "propertiesStack", property);}
                                    ( tmp = <STRING>           {this.setValue(property, "name", this.getString(tmp.image));}
                                    | tmp = <SINGLE>           {this.setValue(property, "name", this.getSingle(tmp.image));} )
              (   ( <PROPERTYVAL>   ( tmp = <STRING>           {this.setValue(property, "value", this.getString(tmp.image));}
                                    | tmp = <SINGLE>           {this.setValue(property, "value", this.getSingle(tmp.image));} ) )
                | ( <PROPERTYTO>    ( tmp = <ADMINTYPE_STRING> {this.setValue(property, "refAdminType", this.getString(tmp.image));}
                                    | tmp = <ADMINTYPE_SINGLE> {this.setValue(property, "refAdminType", this.getSingle(tmp.image));} )
                                    ( tmp = <STRING>           {this.setValue(property, "refAdminName", this.getString(tmp.image));}
                                    | tmp = <SINGLE>           {this.setValue(property, "refAdminName", this.getSingle(tmp.image));} ) ) )* )

      | (<CODE>                     ( tmp = <MULTILINESTRING>  {this.setValue(_command, "code", this.getString(tmp.image));}
                                    | tmp = <MULTILINESINGLE>  {this.setValue(_command, "code", this.getSingle(tmp.image));} ) )
    )*
}
