/*
 *  This file is part of MxUpdate <http://www.mxupdate.org>.
 *
 *  MxUpdate is a deployment tool for a PLM platform to handle
 *  administration objects as single update files (configuration item).
 *
 *  Copyright (C) 2008-2016 The MxUpdate Team - All Rights Reserved
 *
 *  You may use, distribute and modify MxUpdate under the terms of the
 *  MxUpdate license. You should have received a copy of the MxUpdate
 *  license with this file. If not, please write to <info@mxupdate.org>,
 *  or visit <www.mxupdate.org>.
 *
 */

options {
    JDK_VERSION = "1.6";
    STATIC = false;
    ERROR_REPORTING = true;
    USER_TOKEN_MANAGER = false;
    GENERATE_BOILERPLATE = false;
}

PARSER_BEGIN(PortalParser)
package org.mxupdate.update.userinterface;

import java.lang.reflect.InvocationTargetException;

import org.mxupdate.update.userinterface.helper.ChildRefList_mxJPO.ChannelRef;
import org.mxupdate.update.util.AbstractParser_mxJPO;
import org.mxupdate.update.util.AbstractParser_mxJPO.ParseException;
import org.mxupdate.update.util.AbstractParser_mxJPO.SimpleCharStream;
import org.mxupdate.update.util.AbstractParser_mxJPO.Token;
import org.mxupdate.update.util.AbstractParser_mxJPO.TokenMgrError;
import org.mxupdate.update.util.AdminPropertyList_mxJPO.AdminProperty;

/**
 * Parses the update format for {@link Portal_mxJPO}.
 */
@SuppressWarnings("unused")
class PortalParser
    extends AbstractParser_mxJPO<Portal_mxJPO>
{
}
PARSER_END(PortalParser)

<*> SKIP :
{
  " " | "\t" | "\n" | <("#" (~["\n"])* "\n")>
}

TOKEN : /** first level */
{
      <SYMBOLICNAME:            "symbolicname">                                 : STRING_EXPECTED

    | <DESCRIPTION:             "description">                                  : MULTILINESTRING_EXPECTED

    | <HIDDEN_TRUE:             "hidden">
    | <HIDDEN_FALSE:            "!hidden">

    | <ALT:                     "alt">                                          : STRING_EXPECTED

    | <HREF:                    "href">                                         : STRING_EXPECTED

    | <LABEL:                   "label">                                        : STRING_EXPECTED

    | <SETTING:                 ("add" ([" ","\t","\n","\r"])+ )? "setting">    : SETTING_EXPECTED

    | <CHANNEL:                 "channel">                                      : STRING_EXPECTED

    | <NEWROW:                  "newrow">
}

/************************************************************** Common String */
<STRING_EXPECTED> TOKEN :
{
      <STRING: ( "\"" ( ~["\u0000"-"\u001f","\"","\\","\u0100"-"\uffff"] | "\\\"" | "\\\\" | "\\{" | "\\}" )* "\"" )> : DEFAULT
    | <SINGLE: ( ~["\u0000"-"\u0020","\"","\\","{","}","\u0100"-"\uffff"] )+> : DEFAULT
}

/*************************************************** Common Multi-Line-String */
<MULTILINESTRING_EXPECTED> TOKEN :
{
      <MULTILINESTRING: ( "\"" ( ~["\u0000"-"\u0009","\u000b"-"\u001f","\"","\\","\u0100"-"\uffff"] | "\\\"" | "\\\\" | "\\{" | "\\}" | "\\n" )* "\"" )> : DEFAULT
    | <MULTILINESINGLE: ( ~["\u0000"-"\u0020","\"","\\","{","}","\u0100"-"\uffff"] )+> : DEFAULT
}

/********************************************************* Setting Key String */
<SETTING_EXPECTED> TOKEN :
{
      <SETTING_STRING: ( "\"" ( ~["\u0000"-"\u001f","\"","\\","\u0100"-"\uffff"] | "\\\"" | "\\\\" | "\\{" | "\\}" )* "\"" )> : STRING_EXPECTED
    | <SETTING_SINGLE:( ~["\u0000"-"\u0020","\"","\\","{","}","\u0100"-"\uffff"] )+> : DEFAULT
}

/******************************************************************* Property */
<DEFAULT> TOKEN :
{
      <PROPERTY:                            "property">                                     : STRING_EXPECTED
    | <PROPERTYTO:                          "to">                                           : ADMINREF_EXPECTED
    | <PROPERTYVAL:                         "value">                                        : STRING_EXPECTED
}

<ADMINREF_EXPECTED> TOKEN :
{
      <ADMINTYPE_STRING : ( "\"" ( ~["\u0000"-"\u001f","\"","\\","\u0100"-"\uffff"] | "\\\"" | "\\\\" | "\\{" | "\\}" )* "\"" )> : STRING_EXPECTED
    | <ADMINTYPE_SINGLE : ( ~["\u0000"-"\u0020","\"","\\","{","}","\u0100"-"\uffff"] )+> : STRING_EXPECTED
}

/**************************************************************************** */

/**
 * Parses one complete menu definition.
 *
 * @param _portal     target portal to update with parsed values
 */
void parse(Portal_mxJPO _portal)
    throws SecurityException, IllegalArgumentException, NoSuchMethodException, InstantiationException, IllegalAccessException, InvocationTargetException
: {
    Token tmp;
    AdminProperty property;
    ChannelRef ref;
    int refRowIdx = 0, refColIdx = 0;
} {
    (   ( <SYMBOLICNAME>            ( tmp = <STRING>           {this.appendValue(_portal, "symbolicNames", this.getString(tmp.image));}
                                    | tmp = <SINGLE>           {this.appendValue(_portal, "symbolicNames", this.getSingle(tmp.image));} ) )

      | (<DESCRIPTION>              ( tmp = <MULTILINESTRING>  {this.setValue(_portal, "description", this.getString(tmp.image));}
                                    | tmp = <MULTILINESINGLE>  {this.setValue(_portal, "description", this.getSingle(tmp.image));} ) )

      | (<HIDDEN_TRUE>                                         {this.setValue(_portal, "hidden", true);} )
      | (<HIDDEN_FALSE>                                        {this.setValue(_portal, "hidden", false);} )

      | (<ALT>                      ( tmp = <STRING>           {this.setValue(_portal, "alt", this.getString(tmp.image));}
                                    | tmp = <SINGLE>           {this.setValue(_portal, "alt", this.getSingle(tmp.image));} ) )

      | (<HREF>                     ( tmp = <STRING>           {this.setValue(_portal, "href", this.getString(tmp.image));}
                                    | tmp = <SINGLE>           {this.setValue(_portal, "href", this.getSingle(tmp.image));} ) )

      | (<LABEL>                    ( tmp = <STRING>           {this.setValue(_portal, "label", this.getString(tmp.image));}
                                    | tmp = <SINGLE>           {this.setValue(_portal, "label", this.getSingle(tmp.image));} ) )

      | (<SETTING>                                             {property = new AdminProperty();this.appendValue(this.getValue(_portal, "properties"), "propertiesStack", property);}
                                    ( tmp = <SETTING_STRING>   {this.setValue(property, "name", "%" + this.getString(tmp.image));}
                                    | tmp = <SETTING_SINGLE>   {this.setValue(property, "name", "%" + this.getSingle(tmp.image));})
                                    ( tmp = <STRING>           {this.setValue(property, "value", this.getString(tmp.image));}
                                    | tmp = <SINGLE>           {this.setValue(property, "value", this.getSingle(tmp.image));} ) )

      | (<CHANNEL>                                             {ref = new ChannelRef();this.setValue(ref, "column", refColIdx++);this.setValue(ref, "row", refRowIdx);this.appendValue(this.getValue(_portal, "children"), "childrenStack", ref);}
                                    ( tmp = <STRING>           {this.setValue(ref, "name", this.getString(tmp.image));}
                                    | tmp = <SINGLE>           {this.setValue(ref, "name", this.getString(tmp.image));} ) )
      | (<NEWROW>                                              {refRowIdx++;refColIdx = 0;} )

      | (<PROPERTY>                                            {property = new AdminProperty();this.appendValue(this.getValue(_portal, "properties"), "propertiesStack", property);}
                                    ( tmp = <STRING>           {this.setValue(property, "name", this.getString(tmp.image));}
                                    | tmp = <SINGLE>           {this.setValue(property, "name", this.getSingle(tmp.image));} )
              (   ( <PROPERTYVAL>   ( tmp = <STRING>           {this.setValue(property, "value", this.getString(tmp.image));}
                                    | tmp = <SINGLE>           {this.setValue(property, "value", this.getSingle(tmp.image));} ) )
                | ( <PROPERTYTO>    ( tmp = <ADMINTYPE_STRING> {this.setValue(property, "refAdminType", this.getString(tmp.image));}
                                    | tmp = <ADMINTYPE_SINGLE> {this.setValue(property, "refAdminType", this.getSingle(tmp.image));} )
                                    ( tmp = <STRING>           {this.setValue(property, "refAdminName", this.getString(tmp.image));}
                                    | tmp = <SINGLE>           {this.setValue(property, "refAdminName", this.getSingle(tmp.image));} ) ) )* )
    )*
}
